buildscript {
    repositories {
        jcenter()
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.10'
        classpath 'gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:0.9'
        classpath 'fabric-loom:fabric-loom.gradle.plugin:0.8.17'
    }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'
apply plugin: 'fabric-loom'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = '16'
    kotlinOptions.freeCompilerArgs += '-Xuse-experimental=kotlin.ExperimentalUnsignedTypes'
    kotlinOptions.freeCompilerArgs += '-Xjvm-default=all'
}

archivesBaseName = project.archives_base_name
group = project.maven_group

apply from: 'https://dblsaiko.net/pub/gradle/autoversion.gradle'

repositories {
    maven { url 'https://maven.dblsaiko.net' }
    maven { url 'https://maven.terraformersmc.com/releases/' }
}

dependencies {
    def optionalProject = { Map<String, Object> depSpec, String projectName, Closure c = {} ->
        def project = findProject(projectName)
        if (project) {
            implementation(project, c)
        } else {
            modImplementation(depSpec, c)
        }
    }

    minecraft group: "com.mojang", name: "minecraft", version: minecraft_version
    mappings group: "net.fabricmc", name: "yarn", version: yarn_mappings, classifier: "v2"
    modImplementation group: "net.fabricmc", name: "fabric-loader", version: loader_version

    modImplementation group: "net.fabricmc.fabric-api", name: "fabric-api", version: fabric_version
    implementation(group: "net.fabricmc", name: "fabric-language-kotlin", version: fabric_kotlin_version) {
        exclude module: 'fabric-loader'
    }

    optionalProject ':hctm-base', group: 'net.dblsaiko', name: 'hctm-base', version: hctm_base_version
    include group: 'net.dblsaiko', name: 'hctm-base', version: hctm_base_version

    modRuntime group: 'net.dblsaiko', name: 'rswires', version: rswires_version

    modRuntime(group: 'net.dblsaiko.qcommon.cfg', name: 'cfg', version: cfg_version) {
        exclude group: 'net.fabricmc.fabric-api'
        exclude module: 'cfg-keys'
    }

    modImplementation(group: 'net.dblsaiko.qcommon.cfg', name: 'cfg-core', version: cfg_core_version) {
        exclude group: 'net.fabricmc.fabric-api'
    }

    modImplementation group: 'net.dblsaiko', name: 'libaddict', version: libaddict_version

    modImplementation group: "net.dblsaiko.qcommon.croco", name: "croco", version: croco_version

    modRuntime group: 'net.dblsaiko', name: 'winwonders', version: '0.1.2-4'
    modRuntime(group: 'com.terraformersmc', name: 'modmenu', version: '2.0.0-beta.7') {
        exclude module: 'fabric-api'
    }
}

processResources {
    inputs.property 'version', project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from 'LICENSE'
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

idea.project?.settings {
    delegateActions {
        delegateBuildRunToGradle = false
        testRunner = org.jetbrains.gradle.ext.ActionDelegationConfig.TestRunner.PLATFORM
    }
}

apply from: 'https://raw.githubusercontent.com/2xsaiko/gradle-tools/master/publish/publish-slim.gradle'
